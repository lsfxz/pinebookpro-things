# Maintainer: Ross Chadwick <hello@rchadwick.co.uk>

pkgname=nordnm-git
_gitname=NordVPN-NetworkManager
provides=('nordnm')
conflicts=('nordnm')
pkgver=0.7.1_r25_g4e9c1ff
pkgrel=1
pkgdesc="A CLI tool for automating the importing, securing and usage of NordVPN OpenVPN servers through NetworkManager."
url="https://github.com/Chadsr/NordVPN-NetworkManager"
depends=('python' 'iputils' 'sudo' 'networkmanager' 'openvpn' 'networkmanager-openvpn' 'python-requests' 'python-numpy' 'systemd')
makedepends=('python-setuptools')
license=('GPL3')
arch=('any')
source=("git+https://github.com/Chadsr/NordVPN-NetworkManager.git"
        "requirements.patch"
        "killswitch.patch"
        "env.patch::https://github.com/Chadsr/NordVPN-NetworkManager/pull/141.patch"
        "ipv6.patch::https://github.com/Chadsr/NordVPN-NetworkManager/pull/142.patch")
md5sums=('SKIP'
         'b0a5d621c735e4c6ad15c6a12fdeb6b3'
         'bb3c81e549ddf996f9975b06d77eb303'
         'ead55ef7b074521e6a9d3017c8a3af7a'
         'a6b98c4ed058b9a62565adeed7314ef2')

pkgver() {
    cd "${srcdir}/${_gitname}"
    git describe  --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/_/g'
}

prepare() {
    cd "${srcdir}/${_gitname}"
    patch -Np1 -i ../requirements.patch
    patch -Np1 -i ../killswitch.patch
    patch -Np1 -i ../env.patch
    patch -Np1 -i ../ipv6.patch
}

build() {
    cd "${srcdir}/${_gitname}"
    python setup.py build
}

package() {
    depends+=()
    cd "${srcdir}/${_gitname}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
